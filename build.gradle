apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    provided "org.jasig.cas:cas-server-core:$casVersion"
    provided "org.springframework.webflow:spring-webflow:$springWebflowVersion"
    compile "net.unicon.cas:cas-addon-registered-services-commons:$casAddonRegisteredServicesCommonsVersion"
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "junit:junit:$junitVersion"
}

javadoc {
    classpath = configurations.compile + configurations.provided
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.1"
}


createReleaseTag.dependsOn uploadArchives

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: 'https://api.bintray.com/maven/uniconiam/maven/cas-addon-registered-services-authorization') {
                authentication(userName: bintrayUsername, password: bintrayAPIKey)
            }
            pom.project {
                name 'CAS addon JSON Services Registry'
                packaging 'jar'
                description 'An Apereo CAS addon API and implementation of coarse-grained server authorization based on registered services configuration.'
                url 'https://github.com/unicon-cas-addons/cas-addon-registered-services-authorization'

                scm {
                    url 'scm:git@github.com:unicon-cas-addons/cas-addon-registered-services-authorization.git'
                    connection 'scm:git@github.comunicon-cas-addons/cas-addon-registered-services-authorization.git'
                    developerConnection 'scm:git@github.com:unicon-cas-addons/cas-addon-registered-services-authorization.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dima767'
                        name 'Dmitriy Kopylenko'
                    }
                }
            }
        }
    }
}
